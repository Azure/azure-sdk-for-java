buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
    }
}

apply plugin: 'java'
apply plugin: 'checkstyle'

version = '1.0.0-SNAPSHOT'

checkstyle {
    toolVersion = "6.9"
    configFile = new File("$rootDir/Tools/checkstyle/checkstyle.xml")
    configProperties = [samedir: "$rootDir/Tools/checkstyle"]
    reportsDir = new File("$rootDir/Tools/checkstyle/reports")
}

dependencies {
    compile 'com.microsoft.rest:client-runtime:1.0.0-beta1'
    testCompile 'junit:junit:4.12'
    testCompile 'junit:junit-dep:4.11'
    deployerJars "org.apache.maven.wagon:wagon-ftp:2.10"
}

uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            snapshotRepository(url: "ftp://waws-prod-bay-005.ftp.azurewebsites.windows.net/site/wwwroot/") {
                authentication(userName: username, password: password)
            }
            pom.setArtifactId "azure-client-runtime"
            pom.project {
                name 'Microsoft Azure AutoRest Runtime for Java'
                description 'This is the client runtime for AutoRest generated Azure Java clients.'
                url 'https://github.com/Azure/autorest'

                scm {
                    url 'scm:git:https://github.com/Azure/AutoRest'
                    connection 'scm:git:git://github.com/Azure/AutoRest.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'microsoft'
                        name 'Microsoft'
                    }
                }
            }
        }
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardError"
    }
}

task versionInfo(type:Exec){
    commandLine 'git rev-parse HEAD'.split()
    ext.versionfile = new File("${projectDir}/.gitrevision")
    standardOutput = new ByteArrayOutputStream()

    doLast {
        versionfile.text = "Azure/autorest#" + standardOutput.toString()
    }
}

javadoc {
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: [javadoc, versionInfo]) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives file: file("${projectDir}/.gitrevision")
}

test {
    reports.getHtml()
    reports.html.destination = file("${projectDir}/../../../TestResults/JavaAzureRuntime")
}

tasks.compileJava.dependsOn 'clean'
